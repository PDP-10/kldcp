COMMENT ⊗   VALID 00002 PAGES
C REC  PAGE   DESCRIPTION
C00001 00001
C00002 00002	.TITLE 11LST
C00004 ENDMK
C⊗;
.TITLE 11LST

R0=%0
R1=%1
R2=%2
R3=%3
R4=%4
R5=%5
SP=%6
PC=%7

PS=177776
LPTCSR=177514
LPTBUF=177516
TENINT=172566

.=1000
NXTPNT:	-1			;POINTER TO NEXT BUFFER FOR 10 TO FILL
CURBUF:	BUF1
BUFSIZ==132.		;NUMBER OF CHARS IN A LPT LINE
BUF1:	.BLKB BUFSIZ
BUF2:	.BLKB BUFSIZ

.=.+400
PDL:

GO:	RESET
	MOV #340,PS
	MOV #PDL,SP
	MOV #14,R0
	JSR PC,LPTCHR
	MOV #BUF1,NXTPNT
	MOV #BUF1,CURBUF
GOW:	TST NXTPNT		;WAIT FOR SOME DATA
	BPL GOW			;10 WILL SET TO -1 WHEN DONE
	MOV CURBUF,R1
	MOV #BUF2,R2
	CMP R1,#BUF1
	BEQ GOW1
	MOV #BUF1,R2
GOW1:	MOV R2,NXTPNT
	MOV R2,CURBUF
	MOV #1,TENINT
	MOV #BUFSIZ,R2
OUTL:	MOVB (R1)+,R0
	BEQ OUTL1
	JSR PC,LPTCHR
OUTL1:	SOB R2,OUTL
	BR GOW

LPTCHR:	TSTB @#LPTCSR		;Wait for device to be ready
	BPL LPTCHR
	MOV R0,@#LPTBUF
	RTS PC

.END GO
